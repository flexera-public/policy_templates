name "Google Unlabeled Resources"
rs_pt_ver 20180301
type "policy"
short_description "Find all Google Cloud resources(disks, images, instances, snapshots, buckets, vpnGateways), missing any of the user provided labels with the option to update the resources with the missing labels. See the [README](https://github.com/flexera-public/policy_templates/tree/master/compliance/google/unlabeled_resources) and [docs.flexera.com/flexera/EN/Automation](https://docs.flexera.com/flexera/EN/Automation/AutomationGS.htm) to learn more."
category "Compliance"
severity "low"
default_frequency "weekly"
info(
  version: "2.5",
  provider: "Google",
  service: "",
  policy_set: "Unlabeled Resources"
)

###############################################################################
# User inputs
###############################################################################

parameter "param_email" do
  type "list"
  label "Email addresses"
  description "Email addresses of the recipients you wish to notify when new incidents are created"
end

parameter "param_label_key" do
  type "list"
  label "List of Labels"
  category "Google"
  allowed_pattern /\S/
  description "List of labels to find resources which are not labeled by given inputs"
  constraint_description <<-EOS
    This is a mandatory field.
  EOS
end

###############################################################################
# Authentication
###############################################################################

# authenticate with Google
credentials "auth_google" do
  schemes "oauth2"
  label "Google"
  description "Select the Google Cloud Credential from the list."
  tags "provider=gce"
end

###############################################################################
# Pagination
###############################################################################

pagination "google_pagination" do
  get_page_marker do
    body_path "nextPageToken"
  end
  set_page_marker do
    query "pageToken"
  end
end


###############################################################################
# Datasources
###############################################################################

datasource "ds_resource_uri" do
  run_script $js_resource_uri, $ds_google_project
end

#get all google project
datasource "ds_google_project" do
  request do
    auth $auth_google
    pagination $google_pagination
    host "cloudresourcemanager.googleapis.com"
    path "/v1/projects/"
    query "filter", "lifecycleState=ACTIVE"
  end
  result do
    encoding "json"
    collect jmes_path(response, "projects[*]") do
      field "projectNumber", jmes_path(col_item,"projectNumber")
      field "projectId", jmes_path(col_item,"projectId")
    end
  end
end

#https://cloud.google.com/compute/docs/reference/rest/v1/instances/list
datasource "ds_all_resourceses" do
  iterate $ds_resource_uri
  request do
    auth $auth_google
    pagination $google_pagination
    host "compute.googleapis.com"
    path join(["/compute/v1/projects/", val(iter_item, "uri")])
    ignore_status [403,404]
  end
  result do
    encoding "json"
    collect jq(response, ".") do
      field "all_res", col_item
      field "uri", val(iter_item, "uri")
    end
  end
end

datasource "ds_storage_buckets" do
  iterate $ds_google_project
  request do
    auth $auth_google
    host "www.googleapis.com"
    path "/storage/v1/b"
    query "projection", "full"
    query "project", val(iter_item,"projectId")
    ignore_status [403,404]
  end
  result do
    encoding "json"
    collect jq(response, ".") do
      field "name", jmes_path(col_item, "name")
      field "all_res", col_item
    end
  end
end

datasource "ds_filtered_resources" do
  run_script $js_filter_resources, $ds_all_resourceses, $ds_storage_buckets, $param_label_key
end

###############################################################################
# Scripts
###############################################################################

script "js_filter_resources", type: "javascript" do
  parameters "ds_all_resourceses","ds_storage_buckets", "param_label_key"
  result "result"
  code <<-EOS
  var result = [];
  var param_label_key_lower = [];
  var all_input_labels;
  for (var j = 0; j < param_label_key.length; j++) {
    param_label_key_lower[j] = param_label_key[j].toString().toLowerCase();
    all_input_labels = all_input_labels + ',' + param_label_key_lower[j];
  }

  _.each(ds_storage_buckets, function (gr) {
    var obj = gr["all_res"];
    if (obj !== undefined && obj.items !== undefined) {
      for (var key in obj.items) {
        var labelOrValueMissing = false;
        var all_missed_label = '';
        var all_missed_values = '';
        if (JSON.stringify(obj.items[key]["labels"]) == undefined) {
          var labels = "none";
        } else {
          var labels = obj.items[key]["labels"];
          var labels_str = JSON.stringify(labels);
        }
        for (var j = 0; j < param_label_key_lower.length; j++) {
          var labelMissing = true
          for (var labelKey in labels) {
            if (labelKey.toLowerCase() == param_label_key_lower[j]){
              labelMissing = false
              if (labels[labelKey] == "" || labels[labelKey] == null) {
                labelOrValueMissing = true;
                if (all_missed_values == "") {
                  all_missed_values = param_label_key_lower[j];
                } else {
                  all_missed_values = all_missed_values + ", " + param_label_key_lower[j];
                }
              }
            }
          }
          if (labelMissing) {
            labelOrValueMissing = true;
            if (all_missed_label == "") {
              all_missed_label = param_label_key_lower[j];
            } else {
              all_missed_label = all_missed_label + ", " + param_label_key_lower[j];
            }
          }
        }
        if (labelOrValueMissing) {
          result.push({
            id: obj.items[key]["id"],
            labels: labels,
            labelFingerprint: "labelFingerprint",
            selfLink: obj.items[key]["selfLink"],
            all_missed_label: all_missed_label,
            all_missed_values: all_missed_values,
            labels_str: labels_str,
            type: "buckets",
            name: obj.items[key]["name"]
          });
        }
      }
    }
  })

  // Iterate through all resources
  _.each(ds_all_resourceses, function (gr) {
    var obj = gr["all_res"];
    if (obj !== undefined && obj.items !== undefined) {
      for (var key in obj.items) {
        var type = gr["uri"].split("/")[2];
        if (gr["uri"].indexOf("aggregated") !== -1) {
          if (obj.items[key]["warning"] == undefined) {
            for (var k in obj.items[key][type]) {
              var labelOrValueMissing = false;
              var all_missed_label = '';
              var all_missed_values = '';
              if (JSON.stringify(obj.items[key][type][k]["labels"]) == undefined) {
                var labels = "none";
              } else {
                var labels = obj.items[key][type][k]["labels"];
                var labels_str = JSON.stringify(labels);
              }
              for (var j = 0; j < param_label_key_lower.length; j++) {
                var labelMissing = true
                for (var labelKey in labels) {
                  if (labelKey.toLowerCase() == param_label_key_lower[j]){
                    labelMissing = false
                    if (labels[labelKey] == "" || labels[labelKey] == null) {
                      labelOrValueMissing = true;
                      if (all_missed_values == "") {
                        all_missed_values = param_label_key_lower[j]
                      } else {
                        all_missed_values = all_missed_values + ", " + param_label_key_lower[j]
                      }
                    }
                  }
                }
                if (labelMissing) {
                  labelOrValueMissing = true;
                  if (all_missed_label == "") {
                    all_missed_label = param_label_key_lower[j];
                  } else {
                    all_missed_label = all_missed_label + ", " + param_label_key_lower[j];
                  }
                }
              }
              if (labelOrValueMissing) {
                result.push({
                  id: obj.items[key][type][k]["id"],
                  labels: labels,
                  labelFingerprint: obj.items[key][type][k]["labelFingerprint"],
                  selfLink: obj.items[key][type][k]["selfLink"],
                  all_missed_label: all_missed_label,
                  all_missed_values: all_missed_values,
                  labels_str: labels_str,
                  type: type,
                  name: obj.items[key][type][k]["name"]
                });
              }
            }
          }
        } else if (gr["uri"].indexOf("global") !== -1) {
          var labelOrValueMissing = false;
          var all_missed_label = '';
          var all_missed_values = '';
          if (JSON.stringify(obj.items[key]["labels"]) == undefined) {
            var labels = "none";
          } else {
            var labels = obj.items[key]["labels"];
            var labels_str = JSON.stringify(labels);
          }
          for (var j = 0; j < param_label_key_lower.length; j++) {
            var labelMissing = true
            for (var labelKey in labels) {
              if (labelKey.toLowerCase() == param_label_key_lower[j]){
                labelMissing = false
                if (labels[labelKey] == "" || labels[labelKey] == null) {
                  labelOrValueMissing = true;
                  if (all_missed_values == "") {
                    all_missed_values = param_label_key_lower[j]
                  } else {
                    all_missed_values = all_missed_values + ", " + param_label_key_lower[j]
                  }
                }
              }
            }
            if (labelMissing) {
              labelOrValueMissing = true;
              if (all_missed_label == "") {
                all_missed_label = param_label_key_lower[j];
              } else {
                all_missed_label = all_missed_label + ", " + param_label_key_lower[j];
              }
            }
          }
          if (labelOrValueMissing) {
            result.push({
              id: obj.items[key]["id"],
              labels: labels,
              labelFingerprint: obj.items[key]["labelFingerprint"],
              selfLink: obj.items[key]["selfLink"],
              all_missed_label: all_missed_label,
              all_missed_values: all_missed_values,
              labels_str: labels_str,
              type: type,
              name: obj.items[key]["name"]
            });
          }
        }
      }
    }
  })
EOS
end

script "js_resource_uri", type: "javascript" do
  parameters "ds_google_project"
  result "result"
  code <<-EOF
  var result = [];
  var endpoints = [
    "/aggregated/disks",
    "/global/externalVpnGateways",
    "/global/images",
    "/aggregated/instances",
    "/global/snapshots",
    "/aggregated/vpnGateways"
  ]
  _.each(ds_google_project, function (project) {
    _.each(endpoints, function(endpoint){
      result.push({uri: project.projectId + endpoint});
    })
  });
EOF
end


###############################################################################
# Policy
###############################################################################

policy "policy_google_resource" do
  validate $ds_filtered_resources do
    summary_template "{{ len data }} Google Resources out of compliance."
    escalate $esc_email
    escalate $esc_label_resources
    check eq(size(data),0)
    export do
      resource_level true
      field "type" do
        label "Resource Type"
      end
      field "name" do
        label "Resource Name"
      end
      field "labels_str" do
        label "Existing Labels"
      end
      field "all_missed_label" do
        label "Missing Label Keys"
      end
      field "all_missed_values" do
        label "Label Keys with Missing Label values"
      end
      field "id" do
        label "Resources Id"
      end
      field "labels" do
        label "All Labels"
      end
      field "selfLink" do
        label "Self Link"
      end
      field "labelFingerprint" do
        label "labelFingerprint"
      end
    end
  end
end

###############################################################################
# Escalations
###############################################################################

escalation "esc_email" do
  automatic true
  label "Send Email"
  description "Send incident email"
  email $param_email
end

escalation "esc_label_resources" do
  automatic false
  label "Label Resources"
  description "Label the selected unlabeled resources"
  parameter "param_labels_to_add" do
    type "list"
    label "Add Labels"
    description "Add Labels to the selected resource"
  end
  run "label_resources", data, $param_labels_to_add, rs_optima_host
end

###############################################################################
# Cloud Workflow
###############################################################################

define label_resources($data, $param_labels_to_add, $$rs_optima_host) return $all_responses do
  $$log = []
  $all_responses = []

  $list_of_labels_to_add_foreach_data = []

  foreach $item in $data do

    $filtered_labels_to_add_obj = {}
    foreach $label in $param_labels_to_add do
      $label_key_to_add = first(split( $tag, "=" ))
      $label_value_to_add = ""
      if $label_key_to_add != last(split( $tag, "="))
        $label_value_to_add = last(split( $tag, "="))
      end
      $filtered_labels_to_add_obj[$label_key_to_add] = $label_value_to_add
    end

    $list_of_labels_to_add_foreach_data << $filtered_labels_to_add_obj

    sub on_error: handle_error($postResponse) do
      $$log << "after split: "+join([$item["selfLink"],"/setLabels"])
      if $item["type"] != "buckets"
        $postResponse = http_post(
          url: join([$item["selfLink"],"/setLabels"]),
          auth: $$auth_google,
          headers: {
            "cache-control": "no-cache",
            "content-type": "application/json"
          },
          body: {
            "labels": $filtered_labels_to_add_obj,
            "labelFingerprint": $item["labelFingerprint"]
          }
        )
      else
        $postResponse = http_patch(
          url: $item["selfLink"],
          auth: $$auth_google,
          headers: {
            "cache-control": "no-cache",
            "content-type": "application/json"
          },
          body: {
            "labels": $filtered_labels_to_add_obj
          }
        )
      end
    end
    $all_responses << $postResponse
  end
end

define handle_error($response) do
  $$log << "after split: "+to_s($response)
  $status_code = $response["code"]
  if $status_code == 404
    $_error_behavior = "skip"
  else
    $_error_behavior = "raise"
  end
end

define sys_log($subject, $detail) do
  # Create empty errors array if doesn't already exist
  if !$$errors
    $$errors = []
  end
  # Check if debug is enabled
  if $$debug
    # Append to global $$errors
    # This is the suggested way to capture errors
    $$errors << "Unexpected error for " + $subject + "\n  " + to_s($detail)
    # If Flexera NAM Zone, create audit_entries [to be deprecated]
    # This is the legacy method for capturing errors and only supported on Flexera NAM
    if $$rs_optima_host == "api.optima.flexeraeng.com"
      # skip_error_and_append is used to catch error if rs_cm.audit_entries.create fails unexpectedly
      $task_label = "Creating audit entry for " + $subject
      sub task_label: $task, on_error: skip_error_and_append($task) do
        rs_cm.audit_entries.create(
          notify: "None",
          audit_entry: {
            auditee_href: @@account,
            summary: $subject,
            detail: $detail
          }
        )
      end # End sub on_error
    end # End if rs_optima_host
  end # End if debug is enabled
end

define skip_error_and_append($subject) do
  $$errors << "Unexpected error for " + $subject + "\n  " + to_s($_error)
  $_error_behavior = "skip"
end
